use std::str::FromStr;

use ast::{Expr,OpCode};

grammar;

Tier<Op, NextTier>: Box<Expr> = {
    <lhs:Tier<Op, NextTier>> <op:Op> <rhs:NextTier> => Box::new(Expr::BinaryOp(lhs, rhs, op)),
    NextTier
};

pub Expression = Tier<ExprOp, Term>;
ExprOp: OpCode = {
    "+" => OpCode::Add,
    "-" => OpCode::Sub,
};

pub Term = Tier<TermOp, Factor>;
TermOp: OpCode = {
    "*" => OpCode::Mul,
    "/" => OpCode::Div,
};

pub Factor: Box<Expr> = { <Num>, "(" <Expression> ")" };

Num: Box<Expr> = r"[0-9]+" => Box::new(Expr::Num(i32::from_str(<>).unwrap()));
